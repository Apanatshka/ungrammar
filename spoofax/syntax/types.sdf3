module types

imports
  tokens
  keywords
  paths-macros
  items
  stmts-exprs

  types-options
  items-options
  stmts-exprs-options

context-free sorts
  Type TypeBoundList TypeBound

  TypeList ConstOrMut

context-free syntax

Type.ArrayType =
  "[" Type ";" Expr "]"

Type.DynTraitType =
  "dyn" TypeBoundList

Type.FnPtrType =
  ConstOption AsyncOption UnsafeOption AbiOption "fn" ParamList RetTypeOption

Type.ForType =
  "for" GenericParamList Type

Type.ImplTraitType =
  "impl" TypeBoundList

Type.InferType =
  "_"

Type.NeverType =
  "!"

Type.ParenType =
  "(" Type ")"

Type =
  Path

Type.PtrType =
  "*" ConstOrMut Type

Type.RefType =
  "&" LifetimeOption MutOption Type

Type.SliceType =
  "[" Type "]"

Type.TupleType =
  "(" fields:TypeList ")"

TypeList.Trailing = {Type ","}+ ","
TypeList.NoTrailing = {Type ","}*

ConstOrMut = KW_CONST
ConstOrMut = KW_MUT

TypeBoundList.Trailing = {Type "+"}+ "+"
TypeBoundList.NoTrailing = {Type "+"}+

TypeBound.LifeTime =
  LIFETIME

TypeBound.TypeBound =
  QuestionMarkOption Type