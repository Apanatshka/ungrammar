module patterns

imports

  paths-macros
  items
  stmts-exprs

  patterns-options
  items-options

context-free sorts
  Pat RecordPatFieldList RecordPatField

  AtPat RecPatFieldList Rest NameRefColon PatList

context-free syntax

Pat.IdentPat =
  Attr* RefOption MutOption Name AtPatOption

Pat.BoxPat =
  "box" Pat

Pat.RestPat =
  ".."

Pat =
  Literal

Pat =
  MacroCall

Pat.Trailing =
  Pat "|" {Pat "|"}+ "|"

Pat.NoTrailing =
  Pat "|" {Pat "|"}+

Pat.ParenPat =
  "(" Pat ")"

Pat =
  Path

Pat.WildcardPat =
  "_"

Pat.RangePat =
  start:Pat op:RangeOp end:Pat

Pat.RecordPat =
  Path RecordPatFieldList

Pat.RefPat =
  "&" MutOption Pat

Pat.SlicePat =
  "[" PatList "]"

Pat.TuplePat =
   "(" fields:PatList ")"

Pat.TupleStructPat =
   Path "(" fields:PatList ")"

AtPat.AtPat = "@" Pat

RecordPatFieldList.RecordPatFieldList =
  "{"
    fields:RecPatFieldList
    RestOption
  "}"

RecPatFieldList.Trailing = {RecordPatField ","}+ ","
RecPatFieldList.NoTrailing = {RecordPatField ","}*

Rest.Rest = ".."
RecordPatField.RecordPatField =
  Attr* NameRefColonOption Pat

NameRefColon.NameRefColon = NameRef ":"

PatList.Trailing = {Pat ","}+ ","
PatList.NoTrailing = {Pat ","}*
