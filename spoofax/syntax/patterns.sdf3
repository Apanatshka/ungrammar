module patterns

imports

  paths-macros
  items
  stmts-exprs

  patterns-options
  items-options

context-free sorts
  Pat LiteralPat IdentPat WildcardPat RangePat RefPat RecordPat RecordPatFieldList
  RecordPatField TupleStructPat TuplePat ParenPat SlicePat PathPat OrPat BoxPat RestPat
  MacroPat

  AtPat RecPatFieldList Rest NameRefColon PatList

context-free syntax

Pat =
  IdentPat

Pat =
  BoxPat

Pat =
  RestPat

Pat =
  LiteralPat

Pat =
  MacroPat

Pat =
  OrPat

Pat =
  ParenPat

Pat =
  PathPat

Pat =
  WildcardPat

Pat =
  RangePat

Pat =
  RecordPat

Pat =
  RefPat

Pat =
  SlicePat

Pat =
  TuplePat

Pat =
  TupleStructPat

LiteralPat =
  Literal

IdentPat.IdentPat =
  Attr* RefOption MutOption Name AtPatOption

AtPat.AtPat = "@" Pat

WildcardPat.WildcardPat =
  "_"

RangePat.RangePat =
  start:Pat op:RangeOp end:Pat

RefPat.RefPat =
  "&" MutOption Pat

RecordPat.RecordPat =
  Path RecordPatFieldList

RecordPatFieldList.RecordPatFieldList =
  "{"
    fields:RecPatFieldList
    RestOption
  "}"

RecPatFieldList.Trailing = {RecordPatField ","}+ ","
RecPatFieldList.NoTrailing = {RecordPatField ","}*

Rest.Rest = ".."
RecordPatField.RecordPatField =
  Attr* NameRefColonOption Pat

NameRefColon.NameRefColon = NameRef ":"

TupleStructPat.TupleStructPat =
   Path "(" fields:PatList ")"

PatList.Trailing = {Pat ","}+ ","
PatList.NoTrailing = {Pat ","}*

TuplePat.TuplePat =
   "(" fields:PatList ")"

ParenPat.ParenPat =
  "(" Pat ")"

SlicePat.SlicePat =
  "[" PatList "]"

PathPat =
  Path

OrPat.Trailing = {Pat "|"}+ "|"
OrPat.NoTrailing = {Pat "|"}+

BoxPat.BoxPat =
  "box" Pat

RestPat.RestPat =
  ".."

MacroPat =
  MacroCall