module items

imports

  tokens
  paths-macros
  stmts-exprs
  types
  patterns

  paths-macros-options
  items-options
  types-options

context-free sorts
  SourceFile Item ItemList Rename UseTree UseTreeList Fn
  Abi ParamList SelfParam Param RetType TypeAlias Struct RecordFieldList
  RecordField TupleFieldList TupleField FieldList Enum VariantList Variant Union
  AdtDef Const Static AssocItemList AssocItem ExternItemList
  ExternItem GenericParamList GenericParam TypeParam ConstParam LifetimeParam
  WhereClause WherePred Visibility Attr

  ItemListOrSemi NameRefOrSelf NameOrWld UseTreeQualifier StarOrUseTreeList UTList AsyncOrConst
  BlockExprOrSemi ParamListInner SelfParamComma ParamListInnerParamList SelfParamInner SelfParamRef
  ParamInner ParamPatType ColonTypeBoundList StructInner RecordFieldListOrSemi RecFieldList
  TupFieldList VarList EqExpr ImplFor GenParamList EqType WherePredList WherePredFor LifetimeOrType
  VisibilityMod VisibilityModInner AttrInner 

context-free syntax

SourceFile.SourceFile =
  ShebangOption
  Attr*
  Item*

Item =
  Const

Item =
  Enum

Item.ExternBlock =
  Attr* Abi ExternItemList

Item.ExternCrate =
  Attr* VisibilityOption
  "extern" "crate" NameRefOrSelf RenameOption ";"

Item =
  Fn

Item.Impl =
  Attr* VisibilityOption
  DefaultOption UnsafeOption
  "impl" ConstOption GenericParamListOption ImplForOption self_ty:Type WhereClauseOption
  AssocItemList

Item =
  MacroCall

Item.Module =
  Attr* VisibilityOption
  "mod" Name
  ItemListOrSemi

Item =
  Static

Item =
  Struct

Item.Trait =
  Attr* VisibilityOption
  UnsafeOption AutoOption
  "trait" Name GenericParamList ColonTypeBoundListOption WhereClause
  AssocItemList

Item =
  TypeAlias

Item =
  Union

Item.Use =
  Attr* VisibilityOption
  "use" UseTree ";"

ItemListOrSemi = ItemList
ItemListOrSemi.Semi = ";"

ItemList.ItemList =
  "{" Attr* Item* "}"

NameRefOrSelf = NameRef
NameRefOrSelf.Self = "self"

Rename.Rename =
  "as" NameOrWld

NameOrWld = Name
NameOrWld.Wld = "_"

UseTree.UseTree =
  UseTreeQualifierOption StarOrUseTreeList

UseTreeQualifier.UseTreeQualifier = PathOption "::"

StarOrUseTreeList.Star = "*"
StarOrUseTreeList = UseTreeList

UseTree.UseTreeRename =
  Path RenameOption

UseTreeList.UseTreeList =
  "{" UTList "}"

UTList.Trailing = {UseTree ","}+ ","
UTList.NoTrailing = {UseTree ","}*

Fn.Fn =
 Attr* VisibilityOption
 DefaultOption AsyncOrConstOption UnsafeOption AbiOption
 "fn" Name GenericParamListOption ParamList RetTypeOption WhereClauseOption
 BlockExprOrSemi

AsyncOrConst.Async = "async"
AsyncOrConst.Const = "const"

BlockExprOrSemi = BlockExpr
BlockExprOrSemi.Semi = ";"

Abi.Abi =
  "extern" StringOption

ParamList.ParamList =
  "(" ParamListInner ")"

ParamListInner.Self = SelfParam
ParamListInner.Multiple = SelfParamCommaOption ParamListInnerParamList

SelfParamComma.SelfParamComma = SelfParam ","

ParamListInnerParamList.Trailing = {Param ","}+ ","
ParamListInnerParamList.NoTrailing = {Param ","}*

SelfParam.SelfParam =
  Attr* SelfParamInner

SelfParamInner.SelfParamRef = SelfParamRefOption MutOption "self"
SelfParamInner.SelfParamTyped = MutOption "self" ":" Type

SelfParamRef.SelfParamRef = "&" LifetimeOption

Param.Param =
  Attr* ParamInner

ParamInner.ParamPat = Pat ParamPatTypeOption
ParamInner = Type
ParamInner.Rest = "..."

ParamPatType.ParamPatType = ":" Type

RetType.RetType =
  "->" Type

TypeAlias.TypeAlias =
  Attr* VisibilityOption
  DefaultOption
  "type" Name GenericParamListOption ColonTypeBoundListOption WhereClauseOption
  "=" Type ";"

ColonTypeBoundList.ColonTypeBoundList = ":" TypeBoundListOption

Struct.Struct =
  Attr* VisibilityOption
  "struct" Name GenericParamListOption
  StructInner

StructInner.RecordStruct = WhereClauseOption RecordFieldListOrSemi
StructInner.TupleStruct = TupleFieldList WhereClauseOption ";"

RecordFieldListOrSemi = RecordFieldList
RecordFieldListOrSemi.Semi = ";"

RecordFieldList.RecordFieldList =
 "{" fields:RecFieldList "}"

RecFieldList.Trailing = {RecordField ","}+ ","
RecFieldList.NoTrailing = {RecordField ","}*

RecordField.RecordField =
  Attr* VisibilityOption
  Name ":" Type

TupleFieldList.TupleFieldList =
  "(" fields:TupFieldList ")"

TupFieldList.Trailing = {TupleField ","}+ ","
TupFieldList.NoTrailing = {TupleField ","}*

TupleField.TupleField =
  Attr* VisibilityOption
  Type

FieldList =
  RecordFieldList

FieldList =
  TupleFieldList

Enum.Enum =
  Attr* VisibilityOption
  "enum" Name GenericParamListOption WhereClauseOption
  VariantList

VariantList.VariantList =
 "{" VarList "}"

VarList.Trailing = {Variant ","}+ ","
VarList.NoTrailing = {Variant ","}*

Variant.Variant =
  Attr* VisibilityOption
  Name FieldList EqExprOption

EqExpr.EqExpr = "=" Expr

Union.Union =
  Attr* VisibilityOption
  "union" Name GenericParamListOption WhereClauseOption
  RecordFieldList

AdtDef =
  Enum

AdtDef =
  Struct

AdtDef =
  Union

Const.Const =
  Attr* VisibilityOption
  DefaultOption
  "const" NameOrWld ":" Type
  "=" body:Expr ";"

Static.Static =
  Attr* VisibilityOption
  StaticOption MutOption Name ":" Type
  "=" body:Expr ";"

AssocItemList.AssocItemList =
  "{" Attr* AssocItem* "}"

AssocItem =
  Const

AssocItem =
  Fn

AssocItem =
  MacroCall

AssocItem =
  TypeAlias

ImplFor.ImplFor = BangOption trait:Type "for"

ExternItemList.ExternItemList =
  "{" Attr* ExternItem* "}"

ExternItem =
  Fn

ExternItem =
  MacroCall

ExternItem =
  Static

GenericParamList.GenericParamList =
  "<" GenParamList ">"

GenParamList.Trailing = {GenericParam ","}+ ","
GenParamList.NoTrailing = {GenericParam ","}*

GenericParam =
  ConstParam

GenericParam =
  LifetimeParam

GenericParam =
  TypeParam

TypeParam.TypeParam =
  Attr* Name ColonTypeBoundListOption
  EqTypeOption

EqType.EqType = "=" default_type:Type

ConstParam.ConstParam =
  Attr* "const" Name ":" Type
  EqExprOption

LifetimeParam.LifetimeParam =
  Attr* LIFETIME ColonTypeBoundListOption

WhereClause.WhereClause =
  "where" predicates:WherePredList

WherePredList.Trailing = {WherePred ","}+ ","
WherePredList.NoTrailing = {WherePred ","}+

WherePred.WherePred =
  WherePredForOption LifetimeOrType ":" TypeBoundList

WherePredFor.WherePredFor = "for" GenericParamList

LifetimeOrType = LIFETIME
LifetimeOrType = Type

Visibility.Visibility =
  "pub" VisibilityModOption

VisibilityMod.VisibilityMod = "(" VisibilityModInner ")"

VisibilityModInner.Super =
  "super"

VisibilityModInner.Self =
  "self"

VisibilityModInner.Crate =
  "crate"

VisibilityModInner.InPath =
  "in" Path

Attr.Attr =
  "#" BangOption "[" Path AttrInnerOption "]"

AttrInner.IsLiteral = "=" Literal
AttrInner = TokenTree